plugins {
    id 'java'
    id 'eclipse'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0.1"
    id 'signing'
    id 'maven-publish'
}

group = 'dev.ursinn.velocity.nici'
version = '1.0-SNAPSHOT'
description = 'VelocityNiciManagement'

repositories {
    mavenCentral()
    maven {
        name = 'velocitypowered-repo'
        url = 'https://nexus.velocitypowered.com/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.1.0'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

// Javadoc
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

// Publishing Stack - Start
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'management'
            from components.java
            pom {
                packaging = 'jar'
                name = 'VelocityNiciManagement'
                description = 'Management Plugin'
                url = 'https://github.com/ursinn/velocity-nici-management'
                inceptionYear = '2022'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ursinn/velocity-nici-management/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ursinn'
                        name = 'Ursin Filli'
                        email = 'mail@ursinn.dev'
                        timezone = '+1'
                        url = 'https://ursinn.dev'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ursinn/velocity-nici-management.git'
                    developerConnection = 'scm:git:git@github.com:ursinn/velocity-nici-management.git'
                    url = 'https://github.com/ursinn/velocity-nici-management'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/ursinn/velocity-nici-management/issues'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhS01Username
                password ossrhS01Password
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

// Publishing Stack - End